# 集合类型
当我们需要将很多相同类型的数据组合在一起的时候，我们就可以使用集合来完成这个任务。

我们内置的集合类型有列表和字典两种。

## 列表
列表使用有序列表存储同一类型的多个值。相同的值可以多次出现在一个列表的不同位置中。

### 定义
我们只需要使用 `{ e1,e2,e3 }` 语法将我们需要的数据括起来，并用 `,` 分割每一个数据，就可以创建一个列表。  
在大部分情况下，数据类型都可以由语言自动推断。

例如：
```
list := { 1,2,3,4,5 }
```
这样便会创建一个包含 `1` 到 `5` 的 `Int` 类型列表。

如果你需要一个明确类型的列表，可以使用构造函数来创建。

列表类型的表示方法是 `[]type`。

例如我们需要一个字符串列表：
```
List := []Str{}         # 空 #
```
#### 数组
如果我们需要使用原生数组类型，可以使用 `[:]type` 来表示。
也可以使用 `Array of(e1,e2,e3)` 直接创建。

例如：
```
Array: [:]Int = Array of(1,2,3,4,5)
```
### 访问
如果我们需要访问列表中的其中一个元素，我们可以用 `标识符[索引]` 来访问。

例如：
```
Prt( List[1] )
```
需要注意的是，在编程语言里，大多数列表起始索引都是从 `0` 开始的，`标识符[0]` 取得的才是第一个元素，往后的元素以此类推。
### 更改元素
如果我们需要更改列表中的其中一个元素，我们可以直接访问该元素，使用赋值语句来更改。

例如：
```
List[0] = 5
```
需要注意的是，我们只能访问已经存在数据的索引，如果不存在，则会出现错误。
### 常用操作
```
List += 1                   # 添加到末尾 #
List.insert(2, 3)           # 插入元素 3 到索引 2 #
List -= 1                   # 删除指定位置元素 #
Length := List.len          # 长度 #
```
## 字典
字典是用来存储无序的相同类型数据的集合，字典每个值（value）都关联唯一的键（key），键作为字典中的这个值数据的标识符。

和列表中的数据项不同，字典中的数据项并没有具体顺序。我们需要通过标识符（键）访问数据，这种方法很大程度上和我们在现实世界中使用字典查字义的方法一样。

字典的键只能使用 `整数` 和 `字符串` 类型。
### 定义
和列表类似，字典也使用 `{}` 定义，不同的是字典的类型是 `key` 和 `value` 的联合类型，形式是 `[key]value`。

例如：
```
Dictionary := {["a"]1, ["b"]2, ["c"]3}
```
这样便会创建一个包含 `a,b,c` 三个条目 的 `[Str]Int` 类型字典。

如果你需要一个明确类型的字典，同样可以使用构造函数来创建。

字典类型的表示方法是 `[type]type`。

例如：
```
DictionaryNumNum := [Int]Int{} # 空 #
```
### 访问
和列表类似，我们也可以使用索引直接访问数据。

例如：
```
Prt( Dictionary["a"] )
```
### 更改元素
和列表类似，我们也可以使用赋值语句来更改元素。

例如：
```
Dictionary["b"] = 5
```
和列表不同的是，如果赋值的是不存在的索引，也不会错误，会直接将值赋予给新的键。
### 常用操作
```
Dictionary += {["d"]11}         # 添加元素 #
Dictionary -= "c"               # 删除指定索引元素 #
Length := Dictionary.len        # 长度 #
```
## 集合
集合用来存储相同类型、没有确定顺序、且不重复的值。 
也就是说当元素顺序不重要时，或者希望确保每个元素只出现一次时，可以使用集合而不是列表。
### 定义
和列表类似，集合也使用 `{}` 定义，形式是 `[value]`。

例如：
```
Set := {["a"], ["b"], ["c"]}
```
这样便会创建一个包含 `a,b,c` 三个条目 的 `[Str]` 类型集合。

如果你需要一个明确类型的集合，同样可以使用构造函数来创建。

集合类型的表示方法是 `[type]`。

例如：
```
Numbers := [Int]{} # 空 #
```
### 常用操作
```
Numbers.add(1)                  # 添加元素 #
Has :=  Numbers.contains(1)     # 判断是否包含 #
Success = Numbers.remove(1)     # 删除元素 #
Length := Numbers.len           # 长度 #
```

### [下一章](judgment.md)

## 本章示例
```
\Demo <- {
    System
}

Main() -> () {
    List1 := {1,2,3,4,5}
    List1 += 6
    List2 := []I8{1,2,1,2}
    Array := [:]I8{1,2,3}

    Dictionary1 := {["a"]1, ["b"]2, ["c"]3}
    Dictionary1["d"] = 4
    Dictionary2 := [I8]I8{[1]1,[2]2,[3]3}

    Set := [Int]{1,2,3}
}
```
